import com.google.gms.googleservices.GoogleServicesPlugin

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.1"
    flavorDimensions "default"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "io.lbry.browser"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1609
        versionName "0.16.9"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
    }

    productFlavors {
        __32bit {
            versionCode android.defaultConfig.versionCode * 10 + 1
            ndk {
                abiFilter "armeabi-v7a"
            }
        }
        __64bit {
            versionCode android.defaultConfig.versionCode * 10 + 2
            ndk {
                abiFilter "arm64-v8a"
            }
        }
    }

    buildTypes {
        debug {
            Properties twitterProps = new Properties()
            twitterProps.load(project.file('twitter.properties').newDataInputStream())
            resValue "string", "TWITTER_CONSUMER_KEY",  "\"${twitterProps.getProperty("twitterConsumerKey")}\""
            resValue "string", "TWITTER_CONSUMER_SECRET", "\"${twitterProps.getProperty("twitterConsumerSecret")}\""
        }
        release {
            Properties twitterProps = new Properties()
            twitterProps.load(project.file('twitter.properties').newDataInputStream())
            resValue "string", "TWITTER_CONSUMER_KEY",  "\"${twitterProps.getProperty("twitterConsumerKey")}\""
            resValue "string", "TWITTER_CONSUMER_SECRET", "\"${twitterProps.getProperty("twitterConsumerSecret")}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}

configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.3.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment:2.3.1'
    implementation 'androidx.navigation:navigation-ui:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.webkit:webkit:1.4.0-rc01'
    implementation 'androidx.camera:camera-core:1.0.0-beta03'
    implementation 'androidx.camera:camera-camera2:1.0.0-beta03'
    implementation 'androidx.camera:camera-lifecycle:1.0.0-beta03'
    implementation 'androidx.camera:camera-view:1.0.0-alpha10'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.squareup.okhttp3:okhttp:4.4.1'
    implementation 'com.google.firebase:firebase-analytics:18.0.0'
    implementation 'com.google.android.gms:play-services-base:17.5.0'
    implementation 'com.google.firebase:firebase-messaging:21.0.0'
    implementation 'com.google.oauth-client:google-oauth-client:1.30.4'

    implementation 'com.android.billingclient:billing:3.0.2'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.11.4'
    implementation 'com.google.android.exoplayer:exoplayer-dash:2.11.4'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.11.4'
    implementation 'com.google.android.exoplayer:extension-cast:2.11.4'
    implementation 'com.google.android.exoplayer:extension-mediasession:2.11.4'

    implementation 'com.google.android:flexbox:2.0.1'

    implementation 'com.hbb20:ccp:2.3.8'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.atlassian.commonmark:commonmark:0.14.0'

    implementation 'com.arthenica:mobile-ffmpeg-full-gpl:4.3.1.LTS'

    implementation 'org.bitcoinj:bitcoinj-tools:0.14.7'
    implementation 'org.java-websocket:Java-WebSocket:1.5.1'

    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    __32bitImplementation 'io.lbry:lbrysdk32:0.85.1'
    __64bitImplementation 'io.lbry:lbrysdk64:0.85.1'
}

apply plugin: 'com.google.gms.google-services'
GoogleServicesPlugin.config.disableVersionCheck = true
